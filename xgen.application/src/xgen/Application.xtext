grammar xgen.Application with xgen.Grammar

import "http://xgen/Grammar/1.0" as xg
import "http://www.eclipse.org/2008/Xtext" as xt
generate application "http://xgen/Application/1.0"

Model:
	applications+=Apply*;

Apply:
	{Application} 'generator' target=[xt::Grammar|QID]
	'{'
	(constructReplacements+=ConstructReplacement |
	ruleReplacements+=RuleReplacement |
	multiplicityAdjustments+=MultiplicityAdjustment)*
	'}';

ConstructReplacement:
	'replace' selector=Construct0 (positioned?='/' position=INT)?
	'in' target=[xt::AbstractRule]
	'with' replacement=Construct0;

MultiplicityAdjustment:
	'adjust' 'multiplicity' (positioned?='/' position=INT)?
	'in' target=[xt::AbstractRule]
	'with' '{' min=INT (upperBounded?=',' max=INT)? '}';

RuleReplacement:
	'replace' target=[xt::AbstractRule]
	'with' replacement=Construct0;

QID:
	ID ('.' ID)*;